plugins {
    id 'java'
    id 'maven-publish'
    id 'application'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

dependencies {
    implementation 'commons-codec:commons-codec:1.14'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

group = 'com.wuriyanto.jcrypsi'
version = '1.0-SNAPSHOT'
description = 'jcrypsi'
java.sourceCompatibility = JavaVersion.VERSION_1_8

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

application {
    mainClass = 'com.wuriyanto.jcrypsi.App'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.named('test') {
    useJUnitPlatform() 
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.wuriyanto.jcrypsi.App'
    }
    archiveBaseName = 'app'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}